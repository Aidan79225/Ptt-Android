// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'project.gradle'
    apply from: 'versions.gradle'
    addRepos(repositories)
    addProjectDependencies(buildscript)
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

allprojects {
    addAllRepos(repositories)
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            trimTrailingWhitespace()
            endWithNewline()
            // 允許 import 路徑使用萬用字元
            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
        java {
            target "src/*/java/**/*.java"
            googleJavaFormat("1.7").aosp()
            // 移除沒用到的 Import
            removeUnusedImports()
            // 刪除多餘的空白
            trimTrailingWhitespace()
            importOrder('android', 'androidx', 'com', 'junit', 'net', 'org', 'java', 'javax')
        }
    }
}